within STTlibrary.Components.Motor;
model EMF3 "Elecromechanical model of Rotor"
  parameter Integer p "stator elecrical poles";
  parameter SI.ElectricalTorqueConstant Kt "shape function constant";
  final parameter SI.ElectricalTorqueConstant K = 2*Kt/(3*p) "Shape Function Factor";

  SI.Voltage Ea "Induced emf from rotor - phase a";
  SI.Voltage Eb "Induced emf from rotor - phase b";
  SI.Voltage Ec "Induced emf from rotor - phase c";
  SI.Voltage Vn "Neutral Point Voltage";
  SI.AngularVelocity w "rotor angular speed";
  SI.ElectricalTorqueConstant Ka "Shape Function factor - phase a";
  SI.ElectricalTorqueConstant Kb "Shape Function factor - phase b";
  SI.ElectricalTorqueConstant Kc "Shape Function factor - phase c";

  Modelica.Electrical.Analog.Interfaces.PositivePin PA annotation (
    Placement(visible = true, transformation(origin = {-99.27, 60.9927}, extent = {{-12, -12}, {12, 12}}, rotation = 0), iconTransformation(origin = {-101.27, 58.9927}, extent = {{-12, -12}, {12, 12}}, rotation = 0)));
  Modelica.Electrical.Analog.Interfaces.PositivePin PB annotation (
    Placement(visible = true, transformation(origin = {-100.686, -0.7299}, extent = {{-12, -12}, {12, 12}}, rotation = 0), iconTransformation(origin = {-100.686, -0.7299}, extent = {{-12, -12}, {12, 12}}, rotation = 0)));
  Modelica.Electrical.Analog.Interfaces.PositivePin PC annotation (
    Placement(visible = true, transformation(origin = {-100.438, -60.4088}, extent = {{-12, -12}, {12, 12}}, rotation = 0), iconTransformation(origin = {-100.438, -60.4088}, extent = {{-12, -12}, {12, 12}}, rotation = 0)));
  Modelica.Mechanics.Rotational.Interfaces.Flange_b shaft annotation (
    Placement(visible = true, transformation(origin = {99.022, 0.42336}, extent = {{-12, -12}, {12, 12}}, rotation = 0), iconTransformation(origin = {101.022, 0.42336}, extent = {{-12, -12}, {12, 12}}, rotation = 0)));

equation
// Kirchoff Laws
  PA.i+PB.i+PC.i = 0;
  PA.v = Ea+Vn;
  PB.v = Eb+Vn;
  PC.v = Ec+Vn;
// Shape Function factors
  Ka = p*K*sin(p*shaft.phi);
  Kb = p*K*sin(p*shaft.phi - 2/3*pi);
  Kc = p*K*sin(p*shaft.phi - 4/3*pi);

  // Induced elecromotive force from rotor
  Ea = Ka*w;
  Eb = Kb*w;
  Ec = Kc*w;
// Torque calculation at rotor shaft
  w = der(shaft.phi);
  shaft.tau = -(Ka*PA.i + Kb*PB.i + Kc*PC.i) "negative because power is generated by rotor";
  annotation (
    Icon(coordinateSystem(grid = {0.1, 0.1}, initialScale = 0.1), graphics={  Rectangle(fillColor = {255, 255, 255}, fillPattern = FillPattern.Solid, extent = {{-100, 100}, {100, -100}}), Text(origin = {-12, -73}, lineColor = {0, 0, 255}, extent = {{-68, -35}, {92, -57}}, textString = "%name"), Text(origin = {-2, 1}, extent = {{-72, 65}, {72, -65}}, textString = "EMF3")}));
end EMF3;
